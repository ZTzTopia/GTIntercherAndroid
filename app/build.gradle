plugins {
    id 'com.android.application'
}

def baseVersionName = "1.0.0-alpha02"
def ciRunNumber = System.getenv("GITHUB_RUN_NUMBER") ?: ""
def shortRev = "git rev-parse --short HEAD".execute().text.toString().trim()
def isDevBuild = "git tag -l v${baseVersionName}".execute().text.isEmpty()

def genVersionCode = "git rev-list --count HEAD".execute().text.toInteger()
def genVersionName =
        // 1.0.0.deadbeef
        // 1.0.0.dev.deadbeef
        // 1.0.0.dev.deadbeef.27
        "${baseVersionName}${isDevBuild ? ".dev" : ""}.${shortRev}${ciRunNumber ? ".${ciRunNumber}" : ""}"

android {
    compileSdk 33
    namespace "com.gt.launcher"

    defaultConfig {
        applicationId 'com.gt.intercher'
        minSdk 21
        targetSdk 33
        versionCode genVersionCode
        versionName "${genVersionName}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // GTIntercher-1.0.0.deadbeef
            // GTIntercher-1.0.0.dev.deadbeef
            // GTIntercher-1.0.0.dev.deadbeef.27
            // GTIntercher-1.0.0.deadbeef-release
            outputFileName = new File("GTIntercher-${genVersionName}${isDevBuild ? "" : "-${variant.buildType.name}"}.apk")
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    implementation 'androidx.appcompat:appcompat:1.5.1'

    // Needed by Growtopia
    implementation 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'
    implementation 'com.appsflyer:af-android-sdk:6.9.4'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.miui.referrer:homereferrer:1.0.0.6'
    implementation 'com.android.billingclient:billing:5.1.0'
    implementation 'com.google.android.gms:play-services-base:18.1.0'
    implementation 'com.tapjoy:tapjoy-android-sdk:12.10.0'
}
